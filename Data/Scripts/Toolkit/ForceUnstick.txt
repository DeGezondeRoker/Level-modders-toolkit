// ----------------------------------
// RSDK Project: Sonic 1 / 2
// Script Description: Force Unstick Object
// Script Author: De Gezonde Roker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.width
private alias object.value1 : object.height

// Player Aliases
private alias object.type : player.type
private alias object.gravity : player.gravity
private alias object.angle : player.angle
private alias object.collisionMode : player.collisionMode
private alias object.speed : player.speed
private alias object.xvel : player.xvel


// ========================
// Events
// ========================

event ObjectUpdate

	temp0 = object.width
	FlipSign(temp0)
	temp1 = object.height
	FlipSign(temp1)
	
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		if player[currentPlayer].gravity == GRAVITY_GROUND
			BoxCollisionTest(C_TOUCH, object.entityPos, temp0, temp1, object.width, object.height, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
			if checkResult == true
				player[currentPlayer].gravity = GRAVITY_AIR
				player[currentPlayer].angle = 0
				player[currentPlayer].collisionMode = CMODE_FLOOR
				player[currentPlayer].speed = player[currentPlayer].xvel
			end if 
		end if
	next
end event


event ObjectDraw
	if player[0].type == TypeName[Debug Mode]
		temp0 = object.xpos
		temp1 = object.ypos

		temp2 = object.propertyValue
		temp4 = temp2
		temp4 &= 0xF0
		temp4 <<= 15
		temp2 >>= 4
		temp2++
		temp6 = temp2

		temp3 = object.propertyValue
		temp3 &= 0x0F
		temp5 = temp3
		temp5 <<= 19
		temp3++

		temp0 -= temp4
		temp4 = temp0
		temp1 -= temp5
		while temp3 > 0
			while temp2 > 0
				DrawSpriteXY(0, temp0, temp1)
				temp0 += 0x100000
				temp2--
			loop
			temp2 = temp6
			temp0 = temp4
			temp1 += 0x100000
			temp3--
		loop
	end if
end event


event ObjectStartup
	LoadSpriteSheet("Global/Items.gif")
	SpriteFrame(-8, -7, 16, 14, 35, 81)
	
	foreach (TypeName[Force Unstick], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].drawOrder = 5
		
		object[arrayPos0].width = object[arrayPos0].propertyValue
		object[arrayPos0].width &= 0xF0
		object[arrayPos0].width += 16
		object[arrayPos0].width >>= 1

		object[arrayPos0].height = object[arrayPos0].propertyValue
		object[arrayPos0].height &= 0xF
		object[arrayPos0].height++
		object[arrayPos0].height <<= 3
	next
end event
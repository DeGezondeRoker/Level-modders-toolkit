// ----------------------------------
// RSDK Project: Sonic 1 / 2
// Script Description: Water Object
// Script Author: De Gezonde Roker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.width
private alias object.value1 : object.height
private alias object.value8 : object.playersInside	// 0000
private alias object.value47 : temp8

// Player Aliases
private alias object.alpha : player.inWater
private alias object.yvel : player.yvel
private alias object.xpos : player.xpos

// ========================
// Events
// ========================

event ObjectUpdate

	temp0 = object.width
	FlipSign(temp0)
	temp1 = object.height
	FlipSign(temp1)
	temp1 += 16
	
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		BoxCollisionTest(C_TOUCH, object.entityPos, temp0, temp1, object.width, object.height, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
		if checkResult == true
			temp2 = object.playersInside
			temp2 >>= currentPlayer
			temp2 &= 1
			if temp2 == 0
				if player[currentPlayer].inWater == 0
					if player[currentPlayer].yvel != 0
						temp7 = object.iypos
						temp7 -= object.height
						temp7 <<= 16
						
						CreateTempObject(TypeName[Water Splash], 0, player[currentPlayer].xpos, temp7)
						object[tempObjectPos].drawOrder = 4
						PlaySfx(SfxName[Water Splash], false)
					end if
				end if
				player[currentPlayer].inWater++
				temp2 = 1
				temp2 <<= currentPlayer
				object.playersInside ^= temp2
			end if
			
				
		else
			temp2 = object.playersInside
			temp2 >>= currentPlayer
			temp2 &= 1
			if temp2 == 1
				player[currentPlayer].inWater--
				if player[currentPlayer].inWater == 0
					if player[currentPlayer].yvel != 0
						temp7 = object.iypos
						temp7 -= object.height
						temp7 <<= 16
						
						CreateTempObject(TypeName[Water Splash], 0, player[currentPlayer].xpos, temp7)
						object[tempObjectPos].drawOrder = 4
						PlaySfx(SfxName[Water Splash], false)
					end if
				end if
				temp2 = 1
				temp2 <<= currentPlayer
				object.playersInside ^= temp2
			end if
		end if
	next
end event


event ObjectDraw
	object.alpha = 0x100
	object.inkEffect = INK_SUB

	temp0 = object.xpos
	temp1 = object.ypos

	temp2 = object.propertyValue
	temp4 = temp2
	temp4 &= 0xF0
	temp4 <<= 15
	temp2 >>= 4
	temp2++
	temp6 = temp2

	temp3 = object.propertyValue
	temp3 &= 0x0F
	temp5 = temp3
	temp5 <<= 19
	temp3++
	temp7 = temp3

	temp0 -= temp4
	temp4 = temp0
	temp1 -= temp5
	temp5 = temp1
	while temp3 > 0
		while temp2 > 0
			DrawSpriteFX(0, FX_INK, temp0, temp1)
			if temp3 == temp7
				temp8 = temp4
				temp8 %= 2
				temp8 += 1
				DrawSpriteXY(temp8, temp0, temp1)
			end if
			temp0 += 0x100000
			temp2--
		loop
		temp2 = temp6
		temp0 = temp4
		temp1 += 0x100000
		temp3--
	loop
end event


event ObjectStartup
	LoadSpriteSheet("Toolkit/Objects.gif")
	
	CheckCurrentStageFolder("Zone01")
	if checkResult == true
		SpriteFrame(-8, -8, 16, 16, 35, 1)
		SpriteFrame(-8, -8, 16, 16, 1, 1)
		SpriteFrame(-8, -8, 16, 16, 18, 1)
	else
		SpriteFrame(-8, -8, 16, 16, 32, 19)
		SpriteFrame(-8, -8, 16, 16, 32, 36)
		SpriteFrame(-8, -8, 16, 16, 32, 36)
	end if
	
	foreach (TypeName[Water], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].drawOrder = 5
		
		object[arrayPos0].width = object[arrayPos0].propertyValue
		object[arrayPos0].width &= 0xF0
		object[arrayPos0].width += 16
		object[arrayPos0].width >>= 1

		object[arrayPos0].height = object[arrayPos0].propertyValue
		object[arrayPos0].height &= 0xF
		object[arrayPos0].height++
		object[arrayPos0].height <<= 3
	next
end event